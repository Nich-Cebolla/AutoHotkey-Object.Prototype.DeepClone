[color=#800000][size=165][b]Description[/b][/size][/color]
Recursively copies an object's properties onto a new object. For all new objects, [c]ObjDeepClone[/c] attempts to set the new object's base to the same base as the subject. See "Limitations" for situations when this may not be possible. For objects that inherit from [c]Map[/c] or [c]Array[/c], clones the items in addition to the properties.

When [c]ObjDeepClone[/c] encounters an object that has been processed already, [c]ObjDeepClone[/c] assigns a reference to the copy of said object, instead of processing the object again.

[color=#800000][size=165][b]Github link[/b][/size][/color]
https://github.com/Nich-Cebolla/AutoHotkey-Object.Prototype.DeepClone

[color=#800000][size=165][b]Parameters[/b][/size][/color]
[list][*][color=#008000][b]{*}[/color] Self[/b] - The object to be deep cloned. If calling this method from an instance, exclude this parameter.
- {Map} [ConstructorParams] - A map of constructor parameters, where the key is the class name (use [c]ObjToBeCloned.__Class[/c] as the key), and the value is an array of values that will be passed to the constructor. Using [c]ConstructorParams[/c] can allow [c]ObjDeepClone[/c] to create correctly-typed objects in cases where normally AHK will not allow setting the type using [c]ObjSetBase()[/c].
[*][color=#008000][b]{Integer}[/color] [ConstructorParams][/b] - A map of constructor parameters, where the key is the class name (use [c]ObjToBeCloned.__Class[/c] as the key), and the value is an array of values that will be passed to the constructor. Using [c]ConstructorParams[/c] can allow [c]ObjDeepClone[/c] to create correctly-typed objects in cases where normally AHK will not allow setting the type using [c]ObjSetBase()[/c].
[*][color=#008000][b]{Integer}[/color] [Depth = -1][/b] - The maximum depth to clone. A value equal to or less than 0 will result in no limit.[/list]

[color=#800000][size=165][b]Limitations[/b][/size][/color]
[list][*][c]ObjDeepClone[/c] may fail to set the correct type in some situations. This limitation can sometimes be avoided by using the parameter [c]ConstructorParams[/c]. [c]ObjDeepClone[/c] sets the object type by following this sequence of actions:
[list][*][c]ObjDeepClone[/c] attempts to create an instance of the object's class.
[*]If this fails (for example, if the constructor requires input parameters), [c]ObjDeepClone[/c] checks if the object inherits from [c]Map[/c] or [c]Array[/c], and creates a new object from the respective class. If the object does not inherit from [c]Map[/c] or [c]Array[/c], then an instance of [c]Object[/c] is created.
[*]The new object's base is re-assigned to be the same as the original's base. This is where failure will occur for some types, because AutoHotkey prevents assigning a base type in situations where the object and the target base object are fundamentally different. If this occurs, [c]ObjDeepClone[/c] continues to deep clone the object, but the new object remains an instance of [c]Object[/c].[/list]
[*]For each of [c]Menu[/c], [c]MenuBar[/c], [c]Gui[/c], [c]Gui.Control[/c] and its derivatives, [c]Func[/c] and its derivatives, [c]ComValue[/c] and its derivatives, [c]Primitive[/c] and its derivatives [*]These cannot be effectively cloned using [c]ObjDeepClone[/c].
[*][c]RegExMatchInfo[/c] objects will not have their subcapture groups copied to the new object.
[*]Attempting to clone a [c]File[/c] object may produce unexpected results.
[*][c]ComValue[/c] objects are ignored by [c]ObjDeepClone[/c]. References to the original object
are set on the new object instead of attempting to clone them.
[*][c]ObjDeepClone[/c] has not been tested using an instance of [c]InputHook[/c].[/list]

[size=135][b]ObjDeepClone[/b][/size]
[code]

Object.Prototype.DefineProp('DeepClone', { Call: ObjDeepClone })
/**
 * @description - Recursively copies an object's properties onto a new object. For all new objects,
 * `ObjDeepClone` attempts to set the new object's base to the same base as the subject. For objects
 * that inherit from `Map` or `Array`, clones the items in addition to the properties.
 * @param {*} Self - The object to be deep cloned. If calling this method from an instance,
 * exclude this parameter.
 * @param {Map} [ConstructorParams] - A map of constructor parameters, where the key is the class
 * name (use `ObjToBeCloned.__Class` as the key), and the value is an array of values that will be
 * passed to the constructor. Using `ConstructorParams` can allow `ObjDeepClone` to create correctly-
 * typed objects in cases where normally AHK will not allow setting the type using `ObjSetBase()`.
 * @param {Integer} [Depth = 0] - The maximum depth to clone. A value equal to or less than 0 will
 * result in no limit.
 * @returns {*}
 */
ObjDeepClone(Self, ConstructorParams?, Depth := 0) {
    GetTarget := IsSet(ConstructorParams) ? _GetTarget2 : _GetTarget1
    PtrList := Map(ObjPtr(Self), Result := GetTarget(Self))
    CurrentDepth := 1
    return _Recurse(Result, Self)

    _Recurse(Target, Subject) {
        CurrentDepth++
        for Prop in Subject.OwnProps() {
            Desc := Subject.GetOwnPropDesc(Prop)
            if Desc.HasOwnProp('Value') {
                Target.DefineProp(Prop, { Value: IsObject(Desc.Value) ? _ProcessValue(Desc.Value) : Desc.Value })
            } else {
                Target.DefineProp(Prop, Desc)
            }
        }
        if Target is Array {
            Target.Length := Subject.Length
            for item in Subject {
                if IsSet(item) {
                    Target[A_Index] := IsObject(item) ? _ProcessValue(item) : item
                }
            }
        } else if Target is Map {
            Target.Capacity := Subject.Capacity
            for Key, Val in Subject {
                if IsObject(Key) {
                    Target.Set(_ProcessValue(Key), IsObject(Val) ? _ProcessValue(Val) : Val)
                } else {
                    Target.Set(Key, IsObject(Val) ? _ProcessValue(Val) : Val)
                }
            }
        }
        CurrentDepth--
        return Target
    }
    _GetTarget1(Subject) {
        try {
            Target := GetObjectFromString(Subject.__Class)()
        } catch {
            if Subject Is Map {
                Target := Map()
            } else if Subject is Array {
                Target := Array()
            } else {
                Target := Object()
            }
        }
        try {
            ObjSetBase(Target, Subject.Base)
        }
        return Target
    }
    _GetTarget2(Subject) {
        if ConstructorParams.Has(Subject.__Class) {
            Target := GetObjectFromString(Subject.__Class)(ConstructorParams.Get(Subject.__Class)*)
        } else {
            try {
                Target := GetObjectFromString(Subject.__Class)()
            } catch {
                if Subject Is Map {
                    Target := Map()
                } else if Subject is Array {
                    Target := Array()
                } else {
                    Target := Object()
                }
            }
            try {
                ObjSetBase(Target, Subject.Base)
            }
        }
        return Target
    }
    _ProcessValue(Val) {
        if Type(Val) == 'ComValue' || Type(Val) == 'ComObject' {
            return Val
        }
        if PtrList.Has(ObjPtr(Val)) {
            return PtrList.Get(ObjPtr(Val))
        }
        if CurrentDepth == Depth {
            return Val
        } else {
            PtrList.Set(ObjPtr(Val), _Target := GetTarget(Val))
            return _Recurse(_Target, Val)
        }
    }

    /**
     * @description -
     * Use this function when you need to convert a string to an object reference, and the object
     * is nested within an object path. For example, we cannot get a reference to the class `Gui.Control`
     * by setting the string in double derefs like this: `obj := %'Gui.Control'%. Instead, we have to
     * traverse the path to get each object along the way, which is what this function does.
     * @param {String} Path - The object path.
     * @returns {*} - The object if it exists in the scope. Else, returns an empty string.
     * @example
     *  class MyClass {
     *      class MyNestedClass {
     *          static MyStaticProp := {prop1_1: 1, prop1_2: {prop2_1: {prop3_1: 'Hello, World!'}}}
     *      }
     *  }
     *  obj := GetObjectFromString('MyClass.MyNestedClass.MyStaticProp.prop1_2.prop2_1')
     *  OutputDebug(obj.prop3_1) ; Hello, World!
     * @
     */
    GetObjectFromString(Path) {
        Split := StrSplit(Path, '.')
        if !IsSet(%Split[1]%)
            return
        OutObj := %Split[1]%
        i := 1
        while ++i <= Split.Length {
            if !OutObj.HasOwnProp(Split[i])
                return
            OutObj := OutObj.%Split[i]%
        }
        return OutObj
    }
}

[/code]


[color=#800000][size=165]Changelog[/size][/color]
[size=120][b]2025-06-09[/b][/size]
[b]Breaking[/b]
[list][*]Renamed "Object.Prototype.DeepClone.ahk" to "ObjDeepClone.ahk".
[*]Renamed [c]OBJECT_DEEPCLONE[/c] to [c]ObjDeepClone[/c].
[*]Removed [c]DeepCloneA[/c]. Its functionality is consolidated in [c]ObjDeepClone[/c].
[*]Changed the default value of [c]Depth[/c] to 0.
[*]Removed dependency [c]GetObjectFromString[/c] and placed the function as a nested function in [c]ObjDeepClone[/c].
[*]Cleaned up code.
[*]Adjusted wording in function description.
[*]Expanded the "Limitations" section of the documentation.
[*]Cleaned up documentation.[/list]

[size=120][b]2025-02-13[/b][/size]
[list][*]Added [c]if Subject.Has(n)[/c] conditional to Array block.[/list]

[size=120][b]2025-02-13[/b][/size]
[list][*]Refactored the function loop.
[*]Removed long comment about __Enum.
[*]Clarified DeepCloneEx description.
[*]Removed unnecessary parameters within _Recurse function statement.
[*]Added [c]Target.Capacity := Subject.Capacity[/c] for both Array and Map blocks.[/list]

[size=120][b]2025-02-11[/b][/size]
[list][*]Fixed a logical error that would cause cloning an array to terminate early when encountering an unset index.[/list]

[size=120][b]2025-02-08[/b][/size]
[list][*]Uploaded library
[*]A bit later, wrote DeepCloneEx and added it. Also separated [c]GetObjectFromString[/c] into its own file.[/list]

